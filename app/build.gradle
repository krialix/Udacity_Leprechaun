apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("./keystore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.yoloo.apps.leprechaun"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['RELEASE_STORE_FILE'])
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
        }
    }

    flavorDimensions 'mode'

    productFlavors {
        free {
            dimension 'mode'
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            signingConfig signingConfigs.release
        }
        paid {
            dimension 'mode'
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
            signingConfig signingConfigs.release
        }
    }
}

def supportLibVersion = "27.1.1"
def archComponentsVersion = "1.1.1"
def autoValueVersion = "1.6.2"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-core-utils:$supportLibVersion"
    implementation "com.android.support:support-compat:$supportLibVersion"
    implementation "com.android.support:support-core-ui:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:animated-vector-drawable:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    // Arch components
    implementation "android.arch.lifecycle:extensions:$archComponentsVersion"
    implementation "android.arch.lifecycle:common-java8:$archComponentsVersion"
    implementation "android.arch.persistence.room:runtime:$archComponentsVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$archComponentsVersion"

    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'

    // Firebase
    freeImplementation 'com.google.firebase:firebase-ads:15.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.1'

    // Other
    implementation 'org.jsoup:jsoup:1.11.3'

    implementation 'com.google.dagger:dagger:2.16'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'

    implementation 'com.github.slashrootv200:retrofit-html-converter:0.0.2'
    implementation 'com.otaliastudios:autocomplete:1.1.0'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    compileOnly "com.google.auto.value:auto-value-annotations:$autoValueVersion"
    annotationProcessor "com.google.auto.value:auto-value:$autoValueVersion"

    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'
